; v80, (C) Kroc Camen 2023-2024, MIT License
; expression parsing / evaulation
;
.BLOCK  "code-expr"

nextExpr:
;===============================================================================
; skip space before parsing an expression:
; (see parseExpr below for parameters)
;
;-------------------------------------------------------------------------------
        ; skip whitespace and return on either the next non-space character,
        ; or a newline/end-of-file (where a comment is taken as a newline)
        call    nextWord

        ; since the expression is expected/required,
        ; newline or end-of-file is an error:
        jp      z,      errParam

        ; fallthrough
        ; ...

parseExpr:
;===============================================================================
; parse an expression from the input file, tokenise it and evaluate it,
; returning the result. if the expression contains forward-references
; it'll be deferred on the heap and carry-flag is returned set
;
; WARN: the first character of the expression is assumed to be read and
;       the first character of the next word following the expression
;       will be read but not used! this allows chaining of expressions,
;       but you must avoid calling `readChar` without first using the
;       character waiting in `[char]`
;
; in:   HL              current heap addr
;
; out:  BC              the result of the expression evaluation
;       cf              carry clear indicates success
;                       carry set indicates the expression includes forward-
;                       references and couldn't be evaluated; the value of BC
;                       is invalid and the expression was pushed to the heap
;       HL              any forward-references to labels will be added to the
;                       heap along with the whole expression
;       A, DE           (clobbered)
;-------------------------------------------------------------------------------
        ; expressions are tokenised into a space below the stack. this is
        ; because any forward-references to labels will need to be pushed to
        ; the heap so we can't write the expr on the heap at the same time!
        ;
        push    HL                      ; preserve heap addr
        ld      HL,     [expr]          ; addr of expr bytecode space
        xor     A                       ; set A to zero
        ld      [@flag],A               ; clear 'defer' flag
        dec     HL                      ; next byte down is start of bytecode
        ld      A,      [char]          ; get current unused input char
        jr      @unary                  ; jump into the parsing loop

        ;-----------------------------------------------------------------------
@flag   .BYTE   $00                     ; defer flag

        ;=======================================================================
        ; add an operator and fall-through to the next operand:
        ;
@next:  dec     HL                      ; move to next token space
        ld      [HL],   A               ; push token in A to expr

        call    nextWord                ; read first character of the next word
        jp      z,      errExpr         ; newline/EOF = unexpected expr end!

        ;-----------------------------------------------------------------------
        ; unary operators:
        ;=======================================================================
        ; an expression must begin with either a value,
        ; or a unary operator and a value
        ;
        ; unary operators can always precede values,
        ; even after an operator, e.g. `$10 + <:label`
        ;
@unary: cp      CHAR_OP_LO              ; lo-byte operator?
        jr      z,      +               ; a value must follow a unary operator
        ;-----------------------------------------------------------------------
        cp      CHAR_OP_HI              ; hi-byte operator?
        jr      z,      +               ; a value must follow a unary operator
        ;-----------------------------------------------------------------------
        cp      CHAR_OP_NOT             ; not operator?
        jr      nz,     @label          ; no unary operators, check for values

        ; a unary operator has been added,
        ; read the next character:
+       dec     HL                      ; move to next token space
        ld      [HL],   A               ; push a unary operator token
        call    nextWord                ; read first character of the next word
        jp      z,      errExpr         ; newline/EOF = unexpected expr end!

        ; after a unary operator, a value must follow
        ; ...

        ;-----------------------------------------------------------------------
        ; values:
        ;=======================================================================
        ; label?
        ;-----------------------------------------------------------------------
@label: cp      CHAR_LABEL              ; a label?
        jr      nz,     @const          ; not a label? try constants
        ld      DE,     label           ; label dictionary entry point
        call    getSymbol               ; try get the label's value

        ; if the expression contains a forward-reference to a label we won't
        ; be able to evaluate the expression now and it needs to be deferred
        ;
        ; NOTE: it is very imporant to be aware that in the case of carry-set,
        ;       `getSymbol` has returned the label-length in BC and the address
        ;       of the symbol on the heap in DE
        ;
        jr      c,      @defer

        ; if the label is known, push a number literal token of its address
        ; and fall through to looking for a following operator token
        jr      nz,     @num

        ; this label has not been seen before but because it's in an expression
        ; this is a forward-reference -- we need to add a placeholder for the
        ; label on the heap before we can defer the whole expression
        ;
        scf                             ; flag to indicate a forward-reference
        call    pushLabel               ; define the label name on the heap

        ; any expression that contains a forward-reference to a label causes
        ; the whole expression to be deferred. instead of the label value,
        ; the label's address in the dictionary is tokenised (`getSymbol`
        ; & `pushLabel` return the symbol addr in DE)
        ;
@defer: dec     HL                      ; move to next token space
        ld      [HL],   CHAR_LABEL      ; push the label token
        dec     HL                      ; move to next token space
        ld      [HL],   D               ; push the label addr hi-byte
        dec     HL                      ; move to next token space
        ld      [HL],   E               ; push the label addr lo-byte

        ld      A,      %10000000       ; set the deferred flag
        ld      [@flag],A

        jr      @op                     ; an operator can follow a value

        ; constant?
        ;-----------------------------------------------------------------------
@const: cp      CHAR_CONST              ; a constant?
        jr      nz,     @hex            ; not a constant? try hex literal

        ld      DE,     const           ; const dictionary entry point
        call    getSymbol               ; try get the constant's value
        jp      z,      errExpr         ; exprssion contains undefined const!

        ; push a number literal token and fallthrough to
        ; looking for a following operator token
        jr      @num

        ; hexadecimal literal?
        ;-----------------------------------------------------------------------
        ; a sigil is required because A-F can't be separated
        ; from instruction names without look-ahead
        ;
@hex:   cp      CHAR_HEX                ; hexadecimal sigil?
        jp      nz,     errExpr         ; expression has no value!!
        call    parseHexNumber          ; parse the hex number into BC

        ; push a literal number token:
        ;
@num:   dec     HL                      ; move to next token space
        ld      [HL],   CHAR_HEX        ; push a number token
        dec     HL                      ; move to next token space
        ld      [HL],   B               ; push the number hi-byte
        dec     HL                      ; move to next token space
        ld      [HL],   C               ; push the number lo-byte

        ; an operator can follow a value
        ; ...

        ;-----------------------------------------------------------------------
        ; operators:
        ;=======================================================================
        ; a value has been added, check for the presence of an operator:
        ; if there's no operator, the expression ends here
        ;
@op:    call    nextWord                ; read first character of the next word
        jr      z,      @end            ; end expr at newline / end-of-file

        cp      CHAR_OP_ADD             ; add operator?
        jr      z,      @next           ; push token to heap, find next operand
        cp      CHAR_OP_SUB             ; subtract operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_MUL             ; multiply operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_DIV             ; divide operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_AND             ; and operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_OR              ; or operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_XOR             ; xor operator?
        jr      z,      @next           ; ...
        cp      CHAR_OP_MOD             ; modulo operator?
        jr      z,      @next           ; ...

        ; whatever it is, it isn't an operator;
        ; fall through and terminate the expression
        ; ...

        ; end of expr:
        ;=======================================================================
        ; TODO: check for an overflow here
@end:   xor     A                       ; (clear carry flag!)
        dec     HL                      ; a null terminator is appended to
        ld      [HL],   A               ;  avoid counting during evaluation
        ld      DE,     [expr]          ; get starting (high) addr of expr

        pop     HL                      ; restore heap addr to HL
        ld      A,      [@flag]         ; get the defer flag
        rla                             ; pop the hi-bit off into carry
        jr      nc,     evalExpr        ; if clear, evaluate the expression

        ; defer the expression
        ;=======================================================================
        ; calculate length of expression; this is needed
        ; to copy the expr to the heap using LDDR
        ;
        ; the expr bytecode is ordered *downwards* in memory, so the difference
        ; from the last address to the starting adress is the length
        ;
        ; the expr buffer runs from $xx7F downwards to $xx20
        ; so an 8-bit subtraction is sufficient
        ;
        ld      B,      A               ; B must be 0 (LDDR uses BC)
        ld      A,      E               ; start with highest addr (expr-start)
        sub     A,      L               ; subtract lowest addr (expr-end)
        ld      C,      A               ; this is the expr length

        ; we need to push a bunch of values to the heap and the fastest way
        ; to write 16-bit values easily is... the stack! simply make the heap
        ; into the stack and we can PUSH our stuff on to it!
        ;
        ; WE DO NOT NEED TO DISABLE INTERRUPTS!!! any interrupt is going to
        ; push to our heap and then pop the address off when it returns,
        ; which has no effect on the values we're pushing
        ;
        ld      [sp],   SP              ; backup stack pointer
        ld      SP,     HL              ; move stack to heap!
        ;-----------------------------------------------------------------------

        ; link this expression to the previous one:
        ; (since the heap address is in SP, we can reuse HL)
        ;
        ld      HL,     [defer]         ; get previous deferred expr address
        push    HL                      ; write to heap
        ld      [defer],SP              ; write heap addr to the entry point!

        ; push the current code-segment position; this is where
        ; the expression result will be filled in after evaluation
        push    IY

        ; push the current virtual program-counter
        ; (for calculating relative jumps)
        push    IX                      ; PC is always stored in IX, not RAM

        ; push the source file line-number
        ld      HL,     [row]
        push    HL
        ; push the source file column-number, plus the number
        ; of bytes the result should occupy in the code-segment
        ld      HL,     [stride_col]
        push    HL

        ; reserve bytes for the expr result:
        ;
        ; this routine is only called if there is an instruction parameter
        ; (constant defines and if-statements cannot be deferred!) which
        ; assumes a minimum of 1 byte, so check for a second byte
        ;
        dec     L                       ; is the stride 1 or 2?
        jr      z,      +               ; if it is 1, skip the 2nd byte
        inc     IY                      ; reserve a byte and increment
        inc     IX                      ;  virtual program-counter
+       inc     IY                      ; reserve an additional byte
        inc     IX                      ;  and increment program-counter

        ;-----------------------------------------------------------------------
        ld      HL,     0               ; there is no `LD HL, SP` (!?), so
        add     HL,     SP              ;  it's quicker to just add SP to 0!
        ld      SP,     [sp]            ; restore stack pointer!

        ; copy expression bytecode to the heap:
        ;
        ; note that LDDR is post-decrement (copy-then-decrement), whereas
        ; the heap is pre-decrement like the stack (decrement-then-copy)
        ;
        dec     HL                      ; fix heap addr for copy-then-DEC
        ex      DE,     HL              ; LDDR requires DE to be destination
        ld      C,      [HL]            ; first byte is length / defer flag
        res     7,      C               ; stip hi-bit (defer flag)
        ld      B,      0               ; LDDR uses full BC count
        ;ld      A,      [stride]        ; before copy, ue the first byte
        ;ld      [HL],   A               ;  to record the parameter size
        lddr                            ; copy BC bytes from [DE] to [HL]!
        ex      DE,     HL              ; swap updated heap addr back to HL
        inc     HL                      ; fix heap addr after copy

        ; because the expression could not be evaluated
        ; we must return carry-flag set to indicate no-value
        scf
        ret


evalExpr:
;===============================================================================
; evaluates an expr:
;
; WARN: this routine DOES NOT CHECK if the expression is deferred!
;       expressions are deferred when parsed from source code; it is assumed
;       once all code is parsed that all labels will have been defined and
;       evaluation will now succeed -- any label that is still deferred is
;       a hard error
;
; in:   DE      addr of expr (could be [`expr`] or heap!)
; out:  BC      result value
;       HL      (preserved)
;       A, DE   (clobbered)
;-------------------------------------------------------------------------------
        ; we won't need to push anything new to the heap
        ; so we preserve the current heap position
        push    HL
        ex      DE,     HL              ; use HL for reading tokens for now
        ld      DE,     0               ; initialise running total

_evalExpr:
        ; we do not use the length byte because we don't want to maintain a
        ; counter register. we can just rely on the null terminator to stop
        ;
        dec     HL                      ; skip length byte

        ;-----------------------------------------------------------------------
@eval:  ld      A,      [HL]            ; read token type
        dec     HL                      ; move to next token space

        ; check for optional unary operator:
        ;
@unary: cp      CHAR_OP_LO              ; the lo-byte unary operator
        jr      z,      @lo
        cp      CHAR_OP_HI              ; the hi-byte unary operator
        jr      z,      @hi
        cp      CHAR_OP_NOT             ; the not unary operator
        jr      z,      @not

        ; if there's no unary operator, fetch the value:
        ; note that if any of the unary operators match, they will
        ; read the value and skip ahead to checking for operators
        call    @value

        ; check for an operator
@op:    ld      A,      [HL]            ; read token type
        dec     HL                      ; move to next token space

        ; ...

        ; all others having been covered, the only
        ; possible token remaining is the terminator
        ;
        xor     A                       ; return carry-clear for success
@end:   pop     HL                      ; restore heap position

        ret

        ; unary operators:
        ;=======================================================================
        ; lo-byte unary operator:
        ;-----------------------------------------------------------------------
@lo:    call    @value                  ; read the next value into BC
        ld      B,      0               ; clear the hi-byte
        jr      @op                     ; check for next operator

        ; hi-byte unary operator:
        ;-----------------------------------------------------------------------
@hi:    call    @value                  ; read the next value into BC
        ld      C,      B               ; swap the hi-byte into the lo-byte
        ld      B,      0               ;  and clear the hi-byte
        jr      @op                     ; check for next operator

        ; not unary operator:
        ;-----------------------------------------------------------------------
@not:   call    @value                  ; read the next value into BC
        ld      A,      B               ; 
        cpl                             ; invert bits in B
        ld      B,      A               ; 
        ld      A,      C               ; 
        cpl                             ; invert bits in C
        ld      C,      A               ; 
        jr      @op                     ; check for next operator

        ; read a value:
        ;=======================================================================
@value: ld      A,      [HL]            ; read token type
        dec     HL                      ; move to next token space
        cp      CHAR_LABEL              ; a label? (that was deferred)
        jr      z,      @label

        ; the only other possible value type is the
        ; number literal so fall through to that
        ; ...

        ; read a number literal:
        ;-----------------------------------------------------------------------
@hex:   ld      C,      [HL]
        dec     HL
        ld      B,      [HL]
        dec     HL

        ret

        ; read a label addr:
        ;-----------------------------------------------------------------------
        ; known labels are always resolved to number literals during parsing,
        ; so the only labels that remain for evaluation are forward-references:
        ; the token value contains the address of the symbol in the dictionary,
        ; but we have to skip over the label name to get to the value
        ;
@label: push    DE                      ; preserve running total

        xor     A                       ; (set A to zero, clear carry-flag)
        ld      E,      [HL]            ; read the symbol address into DE
        dec     HL                      ; ...
        ld      D,      [HL]            ; ..
        dec     HL                      ; .
        dec     DE                      ; decrement symbol addr to length-byte
        ex      DE,     HL              ; switch symbol addr to HL to read regs
        ld      C,      [HL]            ; read symbol-name length
        ld      B,      A               ;  and put in BC (set B to zero)
        sbc     HL,     BC              ; skip over the symbol name
        or      A,      [HL]            ; is the flag byte 0? (=fwd-ref!)
        jp      z,      errLabelUndef   ; label was never defined!
        dec     HL                      ; skip over flag byte
        ld      C,      [HL]            ;
        dec     HL                      ; read label addr value
        ld      B,      [HL]            ;
        dec     HL                      ;

        ex      DE,     HL              ; switch back to expr addr in HL
        pop     DE                      ; restore running total
        ret

@add:   jr      @eval
@sub:   jr      @eval
@mul:   jr      @eval
@div:   jr      @eval
@and:   jr      @eval
@or:    jr      @eval
@xor:   jr      @eval
@mod:   jr      @eval

.ENDB